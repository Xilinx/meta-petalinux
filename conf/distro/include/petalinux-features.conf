IMAGE_FSTYPES += "tar.gz cpio.gz.u-boot jffs2 ext4"

POKY_DEFAULT_DISTRO_FEATURES = "${BUSYBOX_FEATURES} largefile pam ptest"

# Create packages that support both systemd and sysvinit
# but only on arm/arm64 systems, microblaze is sysvinit only
DISTRO_FEATURES_BACKFILL_CONSIDERED:remove:arm = "sysvinit systemd"
DISTRO_FEATURES_BACKFILL_CONSIDERED:remove:aarch64 = "sysvinit systemd"
DISTRO_FEATURES_BACKFILL:append:arm = " systemd"
DISTRO_FEATURES_BACKFILL:append:aarch64 = " systemd"

# BusyBox features
BUSYBOX_FEATURES = " \
	busybox-hd \
	busybox-ftpd \
	busybox-ftp \
	busybox-telnetd \
	busybox-mdev \
	busybox-inetd \
	busybox-httpd \
	"

DISTRO_FEATURES_DEFAULT:microblaze = "ipv4 ipv6 xattr"

# PetaLinux Default Distro features
PETALINUX_DEFAULT_DISTRO_FEATURES = "fbdev opengl openamp x11 ${PETALINUX_MISC_FEATURES}"

# Remove unsupported PetaLinux Default Distro features for microBlaze
PETALINUX_DEFAULT_DISTRO_FEATURES:microblaze = ""

# PetaLinux Miscellaneous features for aarch64
PETALINUX_MISC_FEATURES = "multiarch security tpm virtualization vmsep wayland xen"

# Remove unsupported PetaLinux Miscellaneous features for zynq and microBlaze
PETALINUX_MISC_FEATURES:zynq = ""
PETALINUX_MISC_FEATURES:microblaze = ""

DISTRO_FEATURES = "${DISTRO_FEATURES_DEFAULT} ${POKY_DEFAULT_DISTRO_FEATURES} ${PETALINUX_DEFAULT_DISTRO_FEATURES}"

SKIP_META_VIRT_SANITY_CHECK:zynq = "1"
SKIP_META_SECURITY_SANITY_CHECK:zynq = "1"
SKIP_META_TPM_SANITY_CHECK:zynq = "1"
SKIP_META_VIRT_SANITY_CHECK:microblaze = "1"
SKIP_META_GNOME_SANITY_CHECK:microblaze = "1"
SKIP_META_SECURITY_SANITY_CHECK:microblaze = "1"
SKIP_META_TPM_SANITY_CHECK:microblaze = "1"

VIRTUAL-RUNTIME_dev_manager:microblaze = "busybox-mdev"
VIRTUAL-RUNTIME_login_manager = "busybox"

INIT_MANAGER_DEFAULT = "systemd"
INIT_MANAGER_DEFAULT:microblaze = "sysvinit"
INIT_MANAGER_DEFAULT:zynq = "sysvinit"

INIT_MANAGER ?= "${INIT_MANAGER_DEFAULT}"

# If we enable both systemd and sysvinit DISTRO_FEATURES, we
# need to ensure that the other one is not installed
PACKAGE_EXCLUDE:append = "${@bb.utils.contains('INIT_MANAGER','systemd',' sysvinit',' systemd',d)}"
# We also need to ensure that dev-pkgs doesn't accidently bring it in either
PACKAGE_EXCLUDE_COMPLEMENTARY:append = "${@bb.utils.contains('INIT_MANAGER','systemd',' sysvinit',' libsystemd libudev udev systemd',d)}"

# Update the default to include the vai sdk host packages
# Along with a simple way to turn it off!
PLNX_ADD_VAI_SDK ?= " nativesdk-packagegroup-protobuf-sdk-host"
PLNX_ADD_VAI_SDK:sdkmingw32 = ""
TOOLCHAIN_HOST_TASK:append = "${PLNX_ADD_VAI_SDK}"

PREFERRED_PROVIDER_iasl-native ?= "iasl-native"
PREFERRED_PROVIDER_virtual/boot-bin ?= "xilinx-bootbin"
PREFERRED_PROVIDER_virtual/dtb ?= "device-tree"
PREFERRED_PROVIDER_virtual/runc = "runc-opencontainers"
PREFERRED_PROVIDER_virtual/containerd = "containerd-opencontainers"
PREFERRED_PROVIDER_start-jupyter = "start-jupyternb"
PREFERRED_PROVIDER_start-jupyter:k26 = "start-jupyterlab"

UBOOT_SUFFIX = "bin"

LICENSE_FLAGS_WHITELIST = " \
	commercial_${MLPREFIX}ffmpeg \
	commercial_${MLPREFIX}x264 \
	commercial_${MLPREFIX}gstreamer1.0-omx \
	commercial_${MLPREFIX}libomxil \
	commercial_${MLPREFIX}sox \
	commercial_${MLPREFIX}faac \
	commercial_${MLPREFIX}faad2 \
        xilinx "

