require conf/distro/poky.conf
require conf/distro/include/petalinux-features.conf
require conf/distro/include/petalinux-mirrors.conf
include conf/distro/include/xilinx-mirrors.conf
include conf/distro/include/petalinux-feed.conf

DISTRO = "petalinux"
DISTRO_NAME = "PetaLinux"
DISTRO_VERSION = "${XILINX_VER_MAIN}"
SDK_VENDOR = "-petalinux"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"

MAINTAINER = "Xilinx <meta-xilinx@yoctoproject.org>"

TARGET_VENDOR = "-xilinx"

SANITY_TESTED_DISTROS:append = " \
	ubuntu-16.04 \n \
	ubuntu-18.04 \n \
	ubuntu-20.04 \n \
	centos-7.4 \n \
	centos-7.5 \n \
	centos-7.6 \n \
	centos-7.7 \n \
	centos-7.8 \n \
	centos-7.9 \n \
	centos-8 \n \
	centos-8.1 \n \
	centos-8.2 \n \
	rhel-7.4 \n \
	rhel-7.5 \n \
	rhel-7.6 \n \
	rhel-7.7 \n \
	rhel-7.8 \n \
	rhel-7.9 \n \
	rhel-8.1 \n \
	rhel-8.2 \n \
	"

# Blacklist packages
INHERIT += "blacklist"
PNBLACKLIST[distro-feed-configs] = "Not supported, see petalinux-image-common.inc for feed config."
PNBLACKLIST[qemu-native] = "Only qemu-xilinx-native is support."
PNBLACKLIST[nativesdk-qemu] = "Only nativesdk-qemu is supported."
PNBLACKLIST[qemu-helper-native] = "Only qemu-xilinx-helper-native is supported."

INHERIT += "buildhistory"
BUILDHISTORY_COMMIT = "1"

#INHERIT += "archiver"

COPY_LIC_MANIFEST = "1"
LICENSE_CREATE_PACKAGE = "1"
BB_GENERATE_MIRROR_TARBALLS = "1"

CONNECTIVITY_CHECK_URIS = ""

# Do not include clang in generated SDKs
CLANGSDK ?= "0"

# Make sure PR service is on
#PRSERV_HOST ??= "localhost:0"

PREFERRED_VERSION_xen = "4.16+git%"
PREFERRED_VERSION_xen-tools = "4.16+stable%"

# Configure default users/groups
# Default rules (assumed no debug-tweaks image feature):
# * disabled root login (set by system default)
# * Add a user 'petalinux' with no password
#   - SSH won't allow passwd-less login w/o debug tweaks, so requires console login
#   - Set to immediately expire
#   - Add to the sudoers file
# * Add 'petalinux' user to the audio group
# * Add 'petalinux' user to the video group
IMAGE_CLASSES += "extrausers"
EXTRA_USERS_PARAMS ?= "useradd -p '' petalinux;passwd-expire petalinux;usermod -a -G audio petalinux;usermod -a -G video petalinux"
USERADDEXTENSION:append = " plnx-useradd-sudoers"
EXTRA_USERS_SUDOERS ?= "petalinux ALL=(ALL) ALL;"

