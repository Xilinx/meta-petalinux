require conf/distro/poky.conf
require conf/distro/include/petalinux-features.conf
require conf/distro/include/petalinux-mirrors.conf

DISTRO = "petalinux"
DISTRO_NAME = "PetaLinux"
DISTRO_VERSION = "${XILINX_VER_MAIN}"
SDK_VENDOR = "-petalinux"
SDK_VERSION := "${@'${DISTRO_VERSION}'.replace('snapshot-${DATE}','snapshot')}"

MAINTAINER = "Xilinx <meta-xilinx@yoctoproject.org>"

TARGET_VENDOR = "-xilinx"

SANITY_TESTED_DISTROS_append = " \
	ubuntu-16.04 \n \
	centos-7.2 \n \
	centos-8 \n \
	rhel-7.4 \n \
	rhel-7.5 \n \
	rhel-7.6 \n \
	rhel-7.7 \n \
	"

# Blacklist packages
INHERIT += "blacklist"
PNBLACKLIST[distro-feed-configs]  = "Not supported, see petalinux-image-common.inc for feed config."

INHERIT += "buildhistory"
BUILDHISTORY_COMMIT = "1"

#INHERIT += "archiver"

COPY_LIC_MANIFEST = "1"
LICENSE_CREATE_PACKAGE = "1"
BB_GENERATE_MIRROR_TARBALLS = "1"

CONNECTIVITY_CHECK_URIS = ""

PREFERRED_VERSION_opencv = "3.4%"

# Do not include clang in generated SDKs
CLANGSDK ?= "0"
